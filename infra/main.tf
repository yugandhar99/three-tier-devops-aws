module "network" {
  source = "./modules/network"

  name_prefix = local.project_name
  cidr        = local.network_cidr
  azs_count   = var.azs_count
}

module "domain" {
  source = "./modules/domain"
  providers = {
    aws.us_east_1 = aws.us_east_1
  }

  hosted_zone_name = var.hosted_zone_name
  # Create certificates first, records will be added separately
  certificate_domains = {
    frontend = {
      domains                = var.frontend_domains
      use_us_east_1_provider = true # Required for CloudFront
    }
    backend = {
      domains                = var.backend_domains
      use_us_east_1_provider = false
    }
  }
}

module "db" {
  source = "./modules/db"

  name_prefix              = local.project_name
  vpc_id                   = module.network.vpc_id
  subnet_ids               = module.network.private_subnets
  source_security_group_id = module.backend.security_group_id
  db_name                  = var.db_name
  username                 = var.db_username
  password                 = var.db_password

  instance_class        = var.db_instance_class
  allocated_storage     = var.db_allocated_storage
  max_allocated_storage = var.db_max_allocated_storage
  backup_retention_period = var.db_backup_retention_period
  multi_az                = var.db_multi_az

  apply_immediately          = var.db_apply_immediately
  skip_final_snapshot        = var.db_skip_final_snapshot
  enable_deletion_protection = var.enable_deletion_protection
}

module "backend" {
  source = "./modules/backend"

  name_prefix    = local.project_name
  vpc_id         = module.network.vpc_id
  subnet_ids     = module.network.private_subnets
  alb_subnet_ids = module.network.public_subnets
  service_cpu    = var.backend_service_cpu
  service_memory = var.backend_service_memory

  db_host     = module.db.db_instance_address
  db_name     = module.db.db_instance_name
  db_username = module.db.db_instance_username
  db_password = var.db_password

  certificate_arn            = module.domain.certificate_arns["backend"]
  enable_deletion_protection = var.enable_deletion_protection
}

module "frontend" {
  source = "./modules/frontend"

  name_prefix     = local.project_name
  domain_names    = var.frontend_domains
  cdn_price_class = var.frontend_cdn_price_class

  certificate_arn            = module.domain.certificate_arns["frontend"]
  enable_deletion_protection = var.enable_deletion_protection
}

# Route53 records for frontend (created after CloudFront distribution)
resource "aws_route53_record" "frontend" {
  zone_id = module.domain.hosted_zone_id
  name    = var.frontend_domains[0]
  type    = "A"

  alias {
    name                   = module.frontend.cdn_domain_name
    zone_id                = module.frontend.cdn_hosted_zone_id
    evaluate_target_health = false
  }
}

# Route53 records for backend
resource "aws_route53_record" "backend" {
  zone_id = module.domain.hosted_zone_id
  name    = var.backend_domains[0]
  type    = "A"

  alias {
    name                   = module.backend.alb_dns_name
    zone_id                = module.backend.alb_zone_id
    evaluate_target_health = true
  }
}
